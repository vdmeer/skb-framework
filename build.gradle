
plugins {
    id "nebula.ospackage" version "5.1.0"
    id "base"
    id "distribution"
}

version = file('src/main/bash/etc/version.txt').text
description = 'SKB-Framework: a flexible development and application framework'


defaultTasks 'deb', 'rpm', 'tgz'

ospackage {
    preInstall file('src/package/preInstall.sh')
    postInstall file('src/package/postInstall.sh')
    preUninstall file('src/package/preUninstall.sh')
    postUninstall file('src/package/postUninstall.sh')

    priority = 'optional'
    maintainer = 'Sven van der Meer <vdmeer.sven@mykolab.com>'
    packageDescription = 'flexible development and application framework'
    distribution = 'development'
    buildHost = "vdm.vandermeer.de"
    license = "Apache 2.0"
    packager = "vdmeer"
    packageGroup = "misc"

    into '/opt/skb/framework'

    from('src/main/bash/bin') {
        fileMode 0555
        into 'bin'
    }
    from('src/main/bash/etc') {
        fileMode 0664
        into 'etc'
    }
    from('src/main/bash/lib') {
        fileMode 0444
        into 'lib'
    }
    from('src/main/bash/man') {
        fileMode 0444
        into 'man'
    }
    from('src/main/bash/doc') {
        fileMode 0444
        into 'doc'
    }
    from('src/main/bash/scenarios') {
        fileMode 0444
        into 'scenarios'
    }
}

task deb(type: Deb) {
    archiveName = project.name + '-' + project.version + '.deb'
    link('/usr/local/bin/skb-framework', '/opt/skb/framework/bin/skb-framework')
    link('/usr/local/share/man/man1/skb-framework.1', '/opt/skb/framework/man/man1/skb-framework.1')
}

task rpm(type: Rpm) {
    archiveName = project.name + '-' + project.version + '.rpm'
    link('/usr/local/bin/skb-framework', '/opt/skb/framework/bin/skb-framework')
    link('/usr/local/share/man/man1/skb-framework.1', '/opt/skb/framework/man/man1/skb-framework.1')
}

task tgz(type: Tar){
    project.ext.startDir = baseName
    archiveName = project.name + '-' + project.version + '-src.tar.gz'
    compression = Compression.GZIP

    from('src/main/bash/bin') {
        fileMode 0555
        into  project.ext.startDir + '/bin'
    }
    from('src/main/bash/etc') {
        fileMode 0664
        into  project.ext.startDir + '/etc'
    }
    from('src/main/bash/lib') {
        fileMode 0664
        into  project.ext.startDir + '/lib'
    }
    from('src/main/bash/man') {
        fileMode 0664
        into  project.ext.startDir + '/man'
    }
    from('src/main/bash/doc') {
        fileMode 0664
        into  project.ext.startDir + '/doc'
    }
    from('src/main/bash/scenarios') {
        fileMode 0664
        into  project.ext.startDir + '/scenarios'
    }
    from('src/package/make') {
        fileMode 0664
        into  project.ext.startDir
    }
}
